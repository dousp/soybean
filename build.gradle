plugins {
    id 'org.springframework.boot' version '2.4.4' apply(false)
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply(false)
    id "io.freefair.lombok" version "5.3.0" apply(false)
}


allprojects {

    repositories {
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://maven.aliyun.com/repository/spring' }
        maven { url 'https://maven.aliyun.com/repository/spring-plugin' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()
    }

    apply plugin: 'idea'
    apply from: "${rootProject.projectDir}/ext.gradle"

    group 'com.dsp'
    version '1.0-SNAPSHOT'


    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

}

subprojects {

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: "io.freefair.lombok"

    if(project.name != 'soy-common' ){
        println project.name + ' add plugin [org.springframework.boot]'
        println project.name + ' add plugin [io.spring.dependency-management]'

        apply plugin: 'org.springframework.boot'
        apply plugin: "io.spring.dependency-management"

        dependencyManagement {
            imports {
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
                mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
            }
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    def lib = this.rootProject.ext.lib;

    dependencies {
        // implementation lib.fastjson
        implementation group: 'com.alibaba', name: 'fastjson', version: '1.2.76'
        implementation group: 'cn.hutool', name: 'hutool-all', version: '5.6.3'

    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

}